apply plugin: 'com.jfrog.bintray'

version = libraryVersion

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
def bintrayUser = System.getenv("BINTRAY_USER")
def bintrayPassword = System.getenv("BINTRAY_PASSWORD")
def gpgPassword = System.getenv("GPG_PASSWORD")

if (project.rootProject.file('local.properties').exists()) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    bintrayUser = properties.getProperty("bintray.user")
    bintrayPassword = properties.getProperty("bintray.apikey")
    gpgPassword = properties.getProperty("bintray.gpg.password")
}
if (project.hasProperty('BINTRAY_USER')) {
    bintrayUser = project.properties['BINTRAY_USER']
}
if (project.hasProperty('BINTRAY_PASSWORD')) {
    bintrayPassword = project.properties['BINTRAY_PASSWORD']
}
if (project.hasProperty('GPG_PASSWORD')) {
    gpgPassword = project.properties['GPG_PASSWORD']
}

bintray {
    user = bintrayUser
    key = bintrayPassword

    configurations = ['archives']
    pkg {
        userOrg = bintrayOrg
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = gpgPassword
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}
